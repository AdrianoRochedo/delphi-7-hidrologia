10 SCREEN 2: CLS
20  GOSUB 8000
30 CLS : KEY OFF
40  PRINT "PACOTE COMPUTACIONAL DE REGIONALIZACAO HIDROLOGICA"
50  PRINT : PRINT "PROGRAMA PARA GRAFICACAO"
60  LOCATE 20, 1: PRINT "LIGUE A IMPRESSORA": INPUT "DIGITE RETURN PARA INICIAR"; F$
70 D$ = CHR$(4)
80 CR$ = CHR$(13)
90 LF$ = CHR$(10)
100 MUDA = 0: IMPR = 0: GRADE = 0: COMPR = 0
110 CP$ = CHR$(33)
120 DIM MG(500, 2), A$(50), V$(99), EE(2), TE(2), TE$(10), HG(2), A(2), B(2), MA(500, 2), P(500, 2), NP(50), D(2, 150), LG(10)
125 DIM COL1(256), COL2(256), COL3(256), COL4(256), COL5(256), COL6(256), COL7(256), COL8(256), A%(8)
130 DIM TL(50), TP(50), QP(50)
140 TE$(1) = "LINEAR  "
150 TE$(2) = "LOG(10) "
160 TE$(3) = "EXTREMOS"
170 TE$(4) = "NORMAL-2"
180 TE$(5) = "GAMA-3  "
190 TE$(6) = "X"
200 TE$(7) = "Y"
210 TE$(8) = "GRADE DECIMAL"
220 TE$(9) = "GRADE LOGARITMICA"
230 TE$(10) = "POTENCIA"
240 TE(1) = 1
250 TE(2) = 1
260 TG = 1
270 LG(2) = .0001
280 LG(3) = 1.0001
290 LG(4) = 1.0001
300 LG(5) = 1.0001
310 X = 0: X1 = 0: Y = 0: Y1 = 0
320 DEF FNL (X)
321 FNL = LOG(X) / LOG(10)
322 END DEF
330 IF X <> X1 AND Y <> Y1 THEN X = X1: Y = Y1
340 DEF FNC (X)
341    FNC = 200 * .07 * LEN(STR$(X))
342 END DEF
350 HG(1) = 20
360 HG(2) = 15
370 D(1, 0) = 1: D(1, 1) = MG(1, 1)
380 D(2, 0) = 1: D(2, 1) = MG(2, 1)
400  REM ******************
410  REM * DEFINE FUNCOES *
420  REM ******************
423  REM ***** Y POTENCIA
424  DEF FNYP (X, ZT)
425        FNYP = (X ^ ZT - 1) / ZT
426  END DEF
427  REM ***** FUNCOES AUXILIARES
428  DEF FNT (X)
429      FNT = X - (2.515517 + .802853 * X + .010328 * (X ^ 2)) / (1 + (1.432788 * X) + (.189269 * (X ^ 2)) + (.001308 * (X ^ 3)))
430  END DEF
431  DEF FNW (X)
432      FNW = SQR(LOG(1 / (X ^ 2)))
433  END DEF
434  REM ***** X LINEAR
440  DEF FNUL (X)
441      FNUL = X
442  END DEF
450  DEF FNVL (X)
452      FNVL = X
453  END DEF
470  REM ***** X LOG
480  DEF FNULO (X)
481      FNULO = LOG(X)
482  END DEF
490  DEF FNVLO (X)
491      FNVLO = LOG(X)
492  END DEF
500  REM ***** X GUMBEL
530  DEF FNUG (X)
531      FNUG = LOG(-LOG(1 - (1 / X)))
532  END DEF
540  DEF FNVG (X)
541      FNVG = LOG(-LOG(1 - (1 / X)))
542  END DEF
560  REM ***** X GAUSS
570  DEF FNUGA (X)
571      STATIC A
572      A = 1 - (1 / X)
573      FNUGA = -1 * (FNT(FNW(A)))
574  END DEF
580  DEF FNVGA (X)
581      STATIC B
582      B = (1 / X)
583      FNVGA = FNT(FNW(B))
584  END DEF
610  REM ***** PEARSON 3
620  DEF FNP (X, AS1)
621      FNP = 2 / (AS1 * ((1 - ((AS1 / 6) ^ 2) + ((((AS1 / 6) * X) ^ 3) - 1))))
622  END DEF
650  REM ***** Y LINEAR
660  DEF FNYL (X)
661      FNYL = X
662  END DEF
680  REM ***** Y LOG
690  DEF FNYLO (X)
691      FNYLO = LOG(X)
692  END DEF
1000 REM ********************
1010 REM * TELA DE CONTROLE *
1020 REM ********************
1030 CLS
1040 PRINT "***** SISTEMA DE GRAFICOS *****"
1050 PRINT
1060 PRINT "<A> ARQUIVOS (ANULA PARAMETROS )"
1070 PRINT
1080 PRINT "            X           Y": PRINT
1100 PRINT "ESCALA"; : LOCATE 7, 12: PRINT "<B> "; TE$(TE(1)): LOCATE 7, 24: PRINT "<C> "; TE$(TE(2))
1110 IF TE(2) = 10 THEN LOCATE 7, 32: PRINT "="; ZT
1120 PRINT "MINIMO"; : LOCATE 8, 12: PRINT "<D> "; MG(1, 1); : LOCATE 8, 24: PRINT "<E> "; MG(2, 1)
1130 PRINT "MAXIMO"; : LOCATE 9, 12: PRINT "<F> "; MG(1, 2): LOCATE 9, 24: PRINT "<G> "; MG(2, 2)
1150 PRINT "COMPR.": LOCATE 10, 12: PRINT "<J> NORMAL"
1160 PRINT "LEGEN."; : LOCATE 11, 12: PRINT "<L> "; TE$(6); : LOCATE 11, 40: PRINT "<M> "; TE$(7)
1170 PRINT
1180 PRINT "<N> PONTOS E/OU LINHAS (TELA 2)"
1190 PRINT
1200 PRINT "<O> FORMULARIO: "; TE$(TG + 7)
1210 PRINT : PRINT "<P> INTERSECAO DOS EIXOS: "; MG(1, 1); " X "; MG(2, 1)
1220 PRINT
1230 PRINT "<Q> DISPARO"
1240 PRINT
1245 PRINT "<R> PRESENCA DE GRADE: "; : IF GRADE = 0 THEN PRINT "AUSENTE  " ELSE PRINT "PRESENTE"
1247 PRINT
1250 PRINT "<S> FIM DE SESSAO"
1260 LOCATE 23, 1: Z$ = INKEY$: IF Z$ = "" THEN GOTO 1260
1270 REM *****  NADA  *****************
1280 IF (ASC(Z$) < 65) OR (ASC(Z$) > 83) THEN GOTO 1260
1290 IF Z$ = "A" THEN GOSUB 3700: GOTO 1000
1300 IF Z$ = "B" THEN E = 1: GOSUB 3100: LOCATE 7, 16: PRINT TE$(TE(1)): GOSUB 2890: GOTO 1260
1310 IF Z$ = "C" THEN E = 2: GOSUB 3100: LOCATE 7, 28: PRINT TE$(TE(2)): GOSUB 2890: GOTO 1260
1320 IF Z$ <> "D" THEN GOTO 1370
1330 LOCATE 8, 16: PRINT "        ": LOCATE 8, 16: INPUT ""; Z: IF TE(1) <> 1 AND Z < LG(TE(1)) THEN GOTO 1330
1340 MG(1, 1) = Z
1345 LOCATE 17, 27: PRINT MG(1, 1); " X "; MG(2, 1); "                          "
1350 IF MG(1, 0) <> MG(1, 1) THEN MG(1, 0) = MG(1, 1)
1360 LOCATE 8, 16: PRINT Z: GOTO 1260
1370 IF Z$ <> "E" THEN GOTO 1420
1380 LOCATE 8, 28: PRINT "           ": LOCATE 8, 28: INPUT ""; Z: IF TE(2) <> 1 AND Z < LG(TE(2)) THEN GOTO 1380
1390 MG(2, 1) = Z
1395 LOCATE 17, 27: PRINT MG(1, 1); " X "; MG(2, 1); "                          "
1400 IF MG(2, 0) <> MG(2, 1) THEN MG(2, 0) = MG(2, 1)
1410 LOCATE 8, 28: PRINT Z: GOTO 1260
1420 IF Z$ <> "F" THEN GOTO 1470
1430 LOCATE 9, 16: PRINT "        ": LOCATE 9, 16: INPUT ""; Z
1440 MG(1, 2) = Z
1450 IF MG(1, 0) > MG(1, 2) THEN MG(1, 0) = MG(1, 2)
1460 LOCATE 9, 16: PRINT Z: GOTO 1260
1470 IF Z$ <> "G" THEN GOTO 1520
1480 LOCATE 9, 28: PRINT "           ": LOCATE 9, 28: INPUT ""; Z
1490 MG(2, 2) = Z
1500 IF MG(2, 0) > MG(2, 2) THEN MG(2, 0) = MG(2, 2)
1510 LOCATE 9, 28: PRINT Z: GOTO 1260
1520 IF Z$ <> "J" THEN GOTO 1560
1530 COMPR = 1 - COMPR: LOCATE 10, 16: IF COMPR = 1 THEN PRINT "AMPLIADO" ELSE PRINT "NORMAL         "
1535 GOTO 1260
1560 IF Z$ <> "K" THEN GOTO 1600
1570 IF TE(1) <> 1 OR TE(2) <> 1 THEN GOTO 1270
1580 LOCATE 10, 28: PRINT "          ": LOCATE 10, 28: INPUT ""; TY: IF TY <= 0 OR TY > MG(2, 2) - MG(2, 1) THEN GOTO 1580
1590 LOCATE 10, 28: PRINT TY: GOTO 1270
1600 IF Z$ = "L" THEN LOCATE 11, 16: INPUT ""; TE$(6): LOCATE 11, 16: PRINT TE$(6); "       ": GOTO 1260
1610 IF Z$ = "M" THEN LOCATE 11, 44: INPUT ""; TE$(7): LOCATE 11, 44: PRINT TE$(7); "        ": GOTO 1260
1620 IF Z$ = "N" THEN GOSUB 4500: GOTO 1000
1630 IF Z$ <> "O" THEN GOTO 1670
1640 IF TG = 1 THEN TG = 2: LOCATE 15, 17: PRINT TE$(TG + 7); "             ": GOTO 1260
1650 LOCATE 15, 17: PRINT TE$(TG + 7); "                   ": GOTO 1260
1670 IF Z$ <> "P" THEN GOTO 1730
1680 IF TE(1) <> 1 OR TE(2) <> 1 THEN LOCATE 17, 27: PRINT MG(1, 0); " X "; MG(2, 0): GOTO 1260
1690 LOCATE 17, 27: PRINT "           ": LOCATE 17, 27: INPUT "X e Y (X,Y)= "; Z1, Z2: IF Z1 < MG(1, 1) OR Z1 > MG(1, 2) OR Z2 > MG(2, 2) THEN GOTO 1690
1700 MG(1, 0) = Z1
1710 MG(2, 0) = Z2
1720 LOCATE 17, 27: PRINT "                                   ": LOCATE 17, 27: PRINT MG(1, 0); " X "; MG(2, 0): GOTO 1260
1730 IF Z$ <> "R" THEN GOTO 1800
1740 GRADE = 1 - GRADE: LOCATE 21, 24: IF GRADE = 1 THEN PRINT "PRESENTE" ELSE PRINT "AUSENTE  "
1750 GOTO 1260
1800 IF Z$ = "S" THEN END
2000 REM ******************
2010 REM * DET PARAMETROS *
2020 REM ******************
2030 IF TE(1) = 1 THEN E = 1: T = TX: GOSUB 3500: REM COMPLETA D(1,N)
2040 IF TE(2) = 1 THEN E = 2: T = TY: GOSUB 3500: REM COMPLETA D(2,N)
2070 IF TE(1) > 1 THEN E = 1: GOSUB 3300
2080 IF TE(2) > 1 THEN E = 2: GOSUB 3300
2090 IF TG = 2 THEN MG(1, 0) = MG(1, 1): MG(2, 0) = MG(2, 1)
2100 IF TE(1) = 5 THEN GOSUB 6400: REM ASSIMETRIA
2110 GOSUB 5000: REM SET A & B
2500 REM **********************
2510 REM * INICIO DA PLOTAGEM *
2520 REM **********************
2530 IF TG = 1 THEN GOSUB 5200
2540 IF TG = 2 THEN GOSUB 5700
2550 FOR I = 1 TO NG
2560 IF TE(1) <> 5 THEN GOSUB 4010: REM LEARQ
2570 FOR J = 1 TO NP - 1
2580 X1 = P(J, 1)
2590 X2 = P(J + 1, 1)
2600 Y1 = P(J, 2)
2610 Y2 = P(J + 1, 2)
2620 IF X1 >= MG(1, 1) AND X1 <= MG(1, 2) AND Y1 >= MG(2, 1) AND Y1 <= MG(2, 2) AND X2 >= MG(1, 1) AND X2 <= MG(1, 2) AND Y2 >= MG(2, 1) AND Y2 <= MG(2, 2) THEN GOTO 2720
2630 IF X1 < MG(1, 1) AND X2 > MG(1, 1) AND X2 <> X1 THEN Y1 = (Y2 - Y1) * ((MG(1, 1) - X1) / (X2 - X1)) + Y1: X1 = MG(1, 1)
2640 IF X1 > MG(1, 2) AND X2 < MG(1, 2) AND X2 <> X1 THEN Y1 = (Y2 - Y1) * ((MG(1, 2) - X1) / (X2 - X1)) + Y1: X1 = MG(1, 2)
2650 IF Y1 < MG(2, 1) AND Y2 > MG(2, 1) AND Y2 <> Y1 THEN X1 = (X2 - X1) * ((MG(2, 1) - Y1) / (Y2 - Y1)) + X1: Y1 = MG(2, 1)
2660 IF Y1 > MG(2, 2) AND Y2 < MG(2, 2) AND Y2 <> Y1 THEN X1 = (X2 - X1) * ((MG(2, 2) - Y1) / (Y2 - Y1)) + X1: Y1 = MG(2, 2)
2670 IF X2 < MG(1, 1) AND X1 > MG(1, 1) AND X2 <> X1 THEN Y2 = (Y2 - Y1) * ((MG(1, 1) - X1) / (X2 - X1)) + Y1: X2 = MG(1, 1)
2680 IF X2 > MG(1, 2) AND X1 < MG(1, 2) AND X2 <> X1 THEN Y2 = (Y2 - Y1) * ((MG(1, 2) - X1) / (X2 - X1)) + Y1: X2 = MG(1, 2)
2690 IF Y2 < MG(2, 1) AND Y1 > MG(2, 1) AND Y2 <> Y1 THEN X2 = (X2 - X1) * ((MG(2, 1) - Y1) / (Y2 - Y1)) + X1: Y2 = MG(2, 1)
2700 IF Y2 > MG(2, 2) AND Y1 < MG(2, 2) AND Y2 <> Y1 THEN X2 = (X2 - X1) * ((MG(2, 2) - Y1) / (Y2 - Y1)) + X1: Y2 = MG(2, 2)
2710 IF X1 < MG(1, 1) OR X1 > MG(1, 2) OR X2 < MG(1, 1) OR X2 > MG(1, 2) OR Y1 < MG(2, 1) OR Y1 > MG(2, 2) OR Y2 < MG(2, 1) OR Y2 > MG(2, 2) THEN GOTO 2730
2720 GOSUB 2800
2730 NEXT J
2740 NEXT I
2750 IF IMPR = 1 THEN GOSUB 7500: IMPR = 0
2760 Z$ = INKEY$: IF Z$ = "" THEN GOTO 2760
2770 GOTO 1000
2800 REM ******************
2810 REM * PLOTA SEGMENTO *
2820 REM ******************
2830 X = X1: Y = Y1: GOSUB 2900: X1 = XX: Y1 = YY
2840 X = X2: Y = Y2: GOSUB 2900: X2 = XX: Y2 = YY
2850 XX = ABS(X2 * ESX) + 5: XX1 = ABS(X1 * ESX) + 5
2860 YY = 195 - ABS(Y2 * ESY): YY1 = 195 - ABS(Y1 * ESY)
2870 GRAF = I: GOSUB 8500
2880 RETURN
2890 IF TE$(TE(E)) = "LOG(10) " THEN LOCATE 15, 17: PRINT " GRADE LOGARITMICA"
2895 RETURN
2900 REM ***************
2910 REM * REDUZ X & Y *
2920 REM ***************
2930 IF MG(1, 1) < 2 THEN GOTO 2931 ELSE GOTO 2940
2931 ON TE(1) GOTO 2932, 2934, 2936, 2938
2932 Z1 = FNUL(MG(1, 1)): GOTO 2940
2934 Z1 = FNULO(MG(1, 1)): GOTO 2940
2936 Z1 = FNUG(MG(1, 1)): GOTO 2940
2938 Z1 = FNUGA(MG(1, 1))
2940 IF MG(1, 1) >= 2 THEN GOTO 2941 ELSE GOTO 2950
2941 ON TE(1) GOTO 2942, 2944, 2946, 2948
2942 Z1 = FNVL(MG(1, 1)): GOTO 2950
2944 Z1 = FNVLO(MG(1, 1)): GOTO 2950
2946 Z1 = FNVG(MG(1, 1)): GOTO 2950
2948 Z1 = FNVGA(MG(1, 1))
2950 IF X < 2 THEN GOTO 2951 ELSE GOTO 2960
2951 ON TE(1) GOTO 2952, 2954, 2956, 2958
2952 Z2 = FNUL(X): GOTO 2960
2954 Z2 = FNULO(X): GOTO 2960
2956 Z2 = FNUG(X): GOTO 2960
2958 Z2 = FNUGA(X)
2960 IF X >= 2 THEN GOTO 2961 ELSE GOTO 2970
2961 ON TE(1) GOTO 2962, 2964, 2966, 2968
2962 Z2 = FNVL(X): GOTO 2970
2964 Z2 = FNVLO(X): GOTO 2970
2966 Z2 = FNVG(X): GOTO 2970
2968 Z2 = FNVGA(X)
2970 IF TE(1) = 5 THEN Z1 = FNP(Z1, AS1): Z2 = FNP(Z2, AS1)
2980 XX = Z2 - Z1
2990 ON TE(2) GOTO 2992, 2994, 2996
2992 YY = FNYL(Y) - FNYL(MG(2, 1))
2993 GOTO 3000
2994 YY = FNYLO(Y) - FNYLO(MG(2, 1))
2995 GOTO 3000
2996 YY = FNYP(Y, ZT) - FNYP(MG(2, 1), ZT)
3000 RETURN
3100 REM *****************
3110 REM * SELECT SCALES *
3120 REM *****************
3130 IF E = 1 AND TE(1) < 5 THEN TE(1) = TE(1) + 1: GOTO 3180
3140 IF E = 1 AND TE(1) = 5 THEN TE(1) = 1
3150 IF E = 2 AND TE(2) = 1 THEN TE(2) = 2: GOTO 3180
3160 IF E = 2 AND TE(2) = 2 THEN TE(2) = 10: LOCATE 23, 1: INPUT "DIGITE POTENCIA DE Y ... "; ZT: GOTO 3180
3170 IF E = 2 AND TE(2) = 10 THEN TE(2) = 1
3180 IF TE(E) > 1 AND TE(E) < 10 AND MG(E, 1) < LG(TE(E)) THEN LOCATE 22, 1: PRINT "LIMITE MINIMO INCOMPATIVEL COM FUNCAO  "; TE$(TE(E)): LOCATE 23, 1: INPUT "DIGITE RETURN "; F$: GOTO 3100
3190 IF E = 2 THEN LOCATE 7, 32: PRINT "                                       "
3200 LOCATE 7, (4 + 12 * E): PRINT TE$(E): IF TE(E) = 10 THEN LOCATE 7, 34: PRINT " = "; ZT
3210 IF TE(1) > 1 OR TE(2) > 1 THEN TG = 2: RETURN
3220 TG = 1
3230 RETURN
3300 REM ********************
3310 REM * POSICIONA BARRAS *
3320 REM ********************
3330 PRINT "-----> CALCULANDO BARRAS..."
3340 I = 1
3350 PRINT "xxxx:", MG(E, 1)
3360 PRINT "YYYY:", MG(E, 2)
3370 D(E, 1) = MG(E, 1)
3380 Z = 10 ^ INT(FNL(MG(E, 1)))
3390 IC = Z
3400 REM *****INICIO DO LOOP
3410 Z = Z + IC
3420 IF Z > MG(E, 1) THEN I = I + 1: D(E, I) = Z
3430 ZL = FNL(Z): DZ = 10 ^ ZL: DI = 10 ^ INT(ZL): IF ABS(DI - DZ) < .0000001 THEN IC = IC * 10
3440 PRINT DZ, DI, Z, IC
3450 IF Z >= MG(E, 2) THEN GOTO 3470
3460 IF Z < MG(E, 2) OR FNL(Z) <> INT(FNL(Z)) THEN GOTO 3400
3470 D(E, 0) = I
3480 MG(E, 2) = D(E, I)
3490 RETURN
3500 REM ********************
3510 REM * COMPLETA D COM T *
3520 REM ********************
3530 PRINT "-----> COMPLETANDO D COM T"
3540 IF T = 0 THEN T = 1
3550 Z = (MG(E, 2) - MG(E, 1)) / T + 1
3560 D(E, 1) = MG(E, 1)
3570 FOR I = 2 TO Z
3580 D(E, I) = D(E, I - 1) + T
3590 NEXT I
3600 D(E, 0) = Z
3610 RETURN
3620 REM ***** INVERSE/NORMAL
3630 PRINT TE$(Z);
3640 RETURN
3650 RETURN
3700 REM ********************
3710 REM * NOMES DE ARQUIVO *
3720 REM ********************
3730 FOR I = 1 TO NG: V$(I) = A$(I): NEXT I: N = NG: I$ = "********** ARQUIVOS DE CURVAS **********": GOSUB 6600
3740 CLS
3750 IF P > 16 THEN PRINT "O NUMERO MAXIMO DE ARQUIVOS E' 16": GOTO 3730
3760 NG = N: FOR I = 1 TO NG: A$(I) = V$(I): NEXT I
3770 FOR I = 1 TO NG
3780 GOSUB 4010
3790 FOR J = 1 TO NP
3800 IF I <> 1 OR J <> 1 GOTO 3850
3810 MA(1, 2) = P(J, 1)
3820 MA(1, 1) = P(J, 1)
3830 MA(2, 2) = P(J, 2)
3840 MA(2, 1) = P(J, 2)
3850 IF P(J, 1) > MA(1, 2) THEN MA(1, 2) = P(J, 1)
3860 IF P(J, 1) < MA(1, 1) THEN MA(1, 1) = P(J, 1)
3870 IF P(J, 2) > MA(2, 2) THEN MA(2, 2) = P(J, 2)
3880 IF P(J, 2) < MA(2, 1) THEN MA(2, 1) = P(J, 2)
3890 NEXT J
3900 CLOSE #1
3910 NEXT I
3920 MG(1, 1) = MA(1, 1)
3930 MG(1, 2) = MA(1, 2)
3940 MG(2, 1) = MA(2, 1)
3950 MG(2, 2) = MA(2, 2)
3960 MG(1, 0) = MA(1, 1)
3970 MG(2, 0) = MA(2, 1)
3980 TX = (MA(1, 2) - MA(1, 1)) / 10
3990 TY = (MA(2, 2) - MA(2, 1)) / 10
4000 RETURN
4010 REM ***********
4020 REM * LEITURA *
4030 REM ***********
4040 OPEN A$(I) FOR INPUT AS #1
4050 INPUT #1, NP, Z
4060 FOR J = 1 TO NP
4070 INPUT #1, P(J, 1), P(J, 2)
4080 NEXT J
4090 CLOSE #1
4100 RETURN
4500 REM ********************
4510 REM * MODO DE PLOTAGEM *
4520 REM ********************
4530 CLS
4540 LOCATE 3, 3: PRINT "SELECAO DO TIPO DE PLOTAGEM"
4550 LOCATE 6, 3: PRINT "GRAFO #"
4560 LOCATE 8, 3: PRINT "PONTO #"
4570 LOCATE 10, 3: PRINT "LINHA #"
4580 FOR I = 1 TO NG
4590 LOCATE 6, (8 + 2 * I)
4600 IF I < 10 THEN PRINT I;
4610 IF I > 9 THEN PRINT CHR$(I + 55);
4620 IF TP(I) = 0 THEN LOCATE 8, (8 + 2 * I): PRINT TP(I)
4630 IF TL(I) = 0 THEN LOCATE 10, (8 + 2 * I): PRINT TL(I)
4640 NEXT I
4650 LOCATE 15, 3: PRINT "GRAFO # (<ESC> PARA VOLTAR AO MENU):"
4660 LOCATE 17, 3: PRINT "PONTO # (TIPO DE SINAL A SER PLOTADO):"
4670 LOCATE 19, 3: PRINT "LINHA # (TIPO DE LINHA A SER PLOTADA):"
4675 LOCATE 23, 3: PRINT "TECLE <I> PARA QUE O GRAFICO SAIA NA IMPRESSORA: "
4680 LOCATE 15, 43: Z$ = INKEY$: IF Z$ = "" THEN GOTO 4680
4690 Z = ASC(Z$): IF Z <> 73 THEN PRINT Z$
4700 IF Z = 27 THEN RETURN
4710 IF Z = 73 THEN IMPR = 1: LOCATE 23, 53: PRINT "ATIVA": GOTO 4680
4720 I = Z: IF I > 64 THEN I = I - 7
4730 I = I - 48: IF I > NG THEN GOTO 4680
4740 LOCATE 17, 43: Z$ = INKEY$: IF Z$ = "" THEN GOTO 4740
4750 Z = ASC(Z$): IF Z = 27 THEN TP(I) = -1: LOCATE 17, 43: PRINT "0": LOCATE 8, (8 + 2 * I): PRINT " 0"
4760 IF (Z <> 27) AND (Z <> 73) THEN TP(I) = Z - 48: LOCATE 17, 42: PRINT TP(I): LOCATE 8, (8 + 2 * I): PRINT TP(I)
4765 IF Z$ = "I" THEN IMPR = 1: LOCATE 23, 53: PRINT "ATIVA": GOTO 4740
4770 LOCATE 19, 43: Z$ = INKEY$: IF Z$ = "" THEN GOTO 4770
4780 Z = ASC(Z$): IF Z = 27 THEN TL(I) = -1: LOCATE 19, 43: PRINT "0": LOCATE 10, (8 + 2 * I): PRINT " 0"
4790 IF (Z <> 27) AND (Z <> 73) THEN TL(I) = Z - 48: LOCATE 19, 42: PRINT TL(I): LOCATE 10, (8 + 2 * I): PRINT TL(I)
4795 IF Z$ = "I" THEN IMPR = 1: LOCATE 23, 53: PRINT "ATIVA": GOTO 4770
4800 GOTO 4680
5000 REM ***************
5010 REM * SET A AND B *
5020 REM ***************
5030 Z = FRE(0)
5040 PRINT "-----> SETANDO A E B"
5050 X = MG(1, 2)
5060 Y = MG(2, 2)
5070 GOSUB 2900
5080 A(1) = X / 635
5090 A(2) = Y / 195
5100 PRINT "A(X): "; A(1)
5110 PRINT "A(Y): "; A(2)
5120 RETURN
5200 REM ***********************
5210 REM * TRACA EIXOS SIMPLES *
5220 REM ***********************
5230 CLS
5240 ESX = 635 / (MG(1, 2) - MG(1, 1)): TXX = TX * ESX
5250 ESY = 195 / (MG(2, 2) - MG(2, 1)): TYY = TY * ESY
5260 TCHAR = 1
5270 LINE (5, 0)-(5, 195)
5280 LINE (5, 195)-(639, 195)
5285 IF GRADE = 1 THEN LINE (5, 0)-(639, 0): LINE (639, 0)-(639, 195)
5290 CONTA = MG(2, 1) + TY
5300 FOR Y = (195 - TYY) TO 0 STEP -TYY
5310 IF GRADE = 1 THEN LINE (5, Y)-(639, Y) ELSE LINE (3, Y)-(7, Y)
5320 CONTA = CONTA + TY
5330 NEXT Y
5340 CONTA = MG(1, 1) + TX
5350 FOR X = 5 + TXX TO 640 STEP TXX
5360 IF GRADE = 1 THEN LINE (X, 0)-(X, 195) ELSE LINE (X, 193)-(X, 197)
5370 CONTA = CONTA + TX
5380 NEXT X
5390 RETURN
5400 REM **********************
5410 REM * LEGENDAS DOS EIXOS *
5420 REM **********************
5430 TCHAR = 1
5440 X = MG(1, 2): Y = MG(2, 0)
5450 IF TG <> 1 THEN Y = MG(2, 1)
5460 XX = XX - L: YY = YY - 60
5470 Y = MG(2, 2): X = MG(1, 0)
5480 IF TG <> 1 THEN X = MG(1, 1)
5490 XX = XX + 10
5500 RETURN
5700 REM ***************
5710 REM * TRACA GRADE *
5720 REM ***************
5730 CLS
5740 IF MG(1, 1) < 2 THEN GOTO 5745 ELSE GOTO 5750
5745 ON TE(1) GOTO 5746, 5747, 5748, 5749
5746 ESX = 634 / (FNUL(MG(1, 2)) - FNUL(MG(1, 1))): GOTO 5750
5747 ESX = 634 / (FNULO(MG(1, 2)) - FNULO(MG(1, 1))): GOTO 5750
5748 ESX = 634 / (FNUG(MG(1, 2)) - FNUG(MG(1, 1))): GOTO 5750
5749 ESX = 634 / (FNUGA(MG(1, 2)) - FNUGA(MG(1, 1)))
5750 IF MG(1, 1) >= 2 THEN GOTO 5755 ELSE GOTO 5760
5755 ON TE(1) GOTO 5756, 5757, 5758, 5759
5756 ESX = 634 / (FNVL(MG(1, 2)) - FNVL(MG(1, 1))): GOTO 5760
5757 ESX = 634 / (FNVLO(MG(1, 2)) - FNVLO(MG(1, 1))): GOTO 5760
5758 ESX = 634 / (FNVG(MG(1, 2)) - FNVG(MG(1, 1))): GOTO 5760
5759 ESX = 634 / (FNVGA(MG(1, 2)) - FNVGA(MG(1, 1)))
5760 ON TE(2) GOTO 5765, 5766, 5767
5765 ESY = 195 / (FNYL(MG(2, 2)) - FNYL(MG(2, 1))): GOTO 5770
5766 ESY = 195 / (FNYLO(MG(2, 2)) - FNYLO(MG(2, 1))): GOTO 5770
5767 ESY = 195 / (FNYP(MG(2, 2), ZT) - FNYP(MG(2, 1), ZT))
5770 F1 = 1: F2 = 2: LINE (5, 0)-(5, 195): LINE (5, 195)-(639, 195)
5780 FOR I = 1 TO D(1, 0)
5790 X = D(1, I): Y = MG(2, F1): GOSUB 2900: XX1 = 5 + ABS(XX * ESX): YY1 = 195 - ABS(YY * ESY)
5800 Y = MG(2, F2): GOSUB 2900: YY = 195 - ABS(YY * ESY)
5810 IF GRADE = 1 THEN LINE (XX1, YY)-(XX1, YY1) ELSE LINE (XX1, 193)-(XX1, 197)
5820 FO = F1: F1 = F2: F2 = FO
5830 NEXT I
5840 FOR I = 1 TO D(2, 0)
5850 Y = D(2, I): X = MG(1, F1): GOSUB 2900: XX1 = 5 + ABS(XX * ESX)
5860 X = MG(1, F2): GOSUB 2900: XX = 5 + ABS(XX * ESX): YY = 195 - ABS(YY * ESY)
5870 IF GRADE = 1 THEN LINE (XX, YY)-(XX1, YY) ELSE LINE (3, YY)-(7, YY)
5880 F0 = F1: F1 = F2: F2 = F0
5890 NEXT
5900 RETURN
6400 REM **************    
6410 REM * ASSIMETRIA *
6420 REM **************
6430 MD = 0
6440 S1 = 0
6450 S2 = 0
6460 FOR J = 1 TO NP
6470 MD = MD + P(J, 2)
6480 NEXT J
6490 MD = MD / NP
6500 FOR J = 1 TO NP
6510 S1 = S1 + (P(J, 2) - MD) ^ 2
6520 S2 = S2 + (P(J, 2) - MD) ^ 3
6530 NEXT J
6540 DP = (S1 / (NP - 1)) ^ .5
6550 AS1 = (NP * S2) / ((NP - 1) * (NP - 2) * (DP ^ 3))
6560 RETURN
6600 REM **********
6610 REM * EDITOR *
6620 REM **********
6630 CLS
6640 PRINT I$
6650 PRINT "       J - JUSTAPOE ITEM"
6660 PRINT "<RETURN> - LISTA ITEM SEGUINTE"
6670 PRINT "     <-> - LISTA ITEM ANTERIOR"
6680 PRINT "       A - ALTERA ITEM CORRENTE"
6690 PRINT "       Q - ENCERRA EDICAO"
6700 PRINT "       I - INSERE ITEM"
6710 PRINT "       D - DELETA ITEM CORRENTE"
6720 PRINT "       S - ARQUIVA LISTA"
6730 PRINT "       C - CARREGA LISTA ARQUIVADA"
6740 P = 0
6750 P = P + 1
6760 IF P > N THEN P = 1
6770 IF P < 1 THEN P = N
6780 LN = CSRLIN
6790 IF LN > 23 THEN LOCATE 11, 1: GOSUB 7380: LOCATE 11, 1
6800 PRINT "#"; P; ": "; V$(P)
6810 Z$ = INKEY$: IF Z$ = "" THEN GOTO 6810
6820 IF Z$ = "A" GOTO 6910
6830 IF Z$ = "Q" THEN PRINT Z$: CLS : RETURN
6840 IF Z$ = "-" THEN P = P - 2
6850 IF Z$ = "I" THEN GOSUB 6950
6860 IF Z$ = "D" THEN GOSUB 7000
6870 IF Z$ = "J" THEN GOSUB 7080
6880 IF Z$ = "S" THEN GOSUB 7130
6890 IF Z$ = "C" THEN GOSUB 7230
6900 GOTO 6750
6910 LN = CSRLIN: IF LN > 23 THEN LOCATE 11, 1: GOSUB 7380: LOCATE 11, 1
6920 INPUT "QUAL O NOVO VALOR "; V$(P)
6930 GOTO 6870
6940 GOTO 6750
6950 FOR J = N TO P STEP -1: V$(J + 1) = V$(J): NEXT J
6960 N = N + 1
6970 LN = CSRLIN: IF LN > 23 THEN LOCATE 11, 1: GOSUB 7380: LOCATE 11, 1
6980 INPUT "QUAL O VALOR A INSERIR "; V$(P)
6990 RETURN
7000 IF N = 0 THEN GOTO 7070
7010 IF N = 1 THEN GOTO 7060
7020 IF N = P THEN GOTO 7060
7030 FOR I = P TO N - 1
7040 V$(I) = V$(I + 1)
7050 NEXT I
7060 N = N - 1
7070 RETURN
7080 REM *** JUSTAPOSICAO ***
7090 N = N + 1: P = N
7100 LN = CSRLIN: IF LN > 23 THEN LOCATE 11, 1: GOSUB 7380: LOCATE 11, 1
7110 PRINT "QUAL O VALOR DO ITEM #"; P; : INPUT " "; V$(P)
7120 P = P - 1: RETURN
7130 REM ***** SALVA CONJUNTO DE NOMES
7140 LN = CSRLIN: IF LN > 23 THEN LOCATE 11, 1: GOSUB 7380: LOCATE 11, 1
7150 INPUT "DE UM NOME PARA ESTE CONJUNTO: "; CJ$
7160 OPEN CJ$ FOR OUTPUT AS #1
7170 PRINT #1, N; 1
7180 FOR I = 1 TO N
7190 PRINT #1, V$(I)
7200 NEXT I
7210 CLOSE #1
7220 RETURN
7230 REM ***** RECUPERA CONJUNTO DE NOMES
7240 ON ERR GOTO 7350
7250 INPUT "QUAL O NOME DO CONJUNTO  "; CJ$
7260 OPEN CJ$ FOR INPUT AS #1
7270 INPUT #1, N: INPUT #1, Z
7280 FOR I = 1 TO N
7290 INPUT #1, V$(I)
7300 NEXT I
7310 CLOSE #1
7320 RETURN
7330 REM ***** DOS ERROR
7340 CLS
7350 CLS : PRINT "ERRO DE DOS. E' PROVAVEL QUE O CONJUNTO QUE VOCE SOLICITOU NAO ESTEJA GRAVADO NO DISQUETE. TECLE QUALQUER COISA: "
7360 Z$ = INKEY$: IF Z$ = "" THEN GOTO 7360
7370 CLS : GOTO 1000
7380 FOR I = 11 TO 23: PRINT "                                                     ": NEXT I: RETURN
7500 REM ***********************
7510 REM *  INICIO DO HARDCOPY *
7520 REM ***********************
7535 IF COMPR = 0 THEN GOTO 9500
7530 OPEN "LPT1:" FOR RANDOM AS #1: WIDTH #1, 255
7540 ESC$ = CHR$(27): DEF SEG = &HB800
7550 PRINT #1, ESC$; "3"; CHR$(3); CHR$(10)
7560 FOR ROW% = 0 TO 99
7585 FOR G = 1 TO 2
7590 LPRINT "                ";
7600 PRINT #1, ESC$; "L"; CHR$(1); CHR$(5);
7610 FOR COL% = 0 TO 79
7620 H% = COL% + (80 * ROW%)
7630 C% = PEEK(H%)
7640 PRINT #1, CHR$(COL1(C%)); CHR$(COL1(C%)); CHR$(COL2(C%)); CHR$(COL2(C%)); CHR$(COL3(C%)); CHR$(COL3(C%)); CHR$(COL4(C%)); CHR$(COL4(C%));
7645 PRINT #1, CHR$(COL5(C%)); CHR$(COL5(C%)); CHR$(COL6(C%)); CHR$(COL6(C%)); CHR$(COL7(C%)); CHR$(COL7(C%)); CHR$(COL8(C%)); CHR$(COL8(C%));
7650 NEXT COL%
7660 PRINT #1, CHR$(0): LPRINT
7665 NEXT G
7668 FOR G = 1 TO 2
7670 LPRINT "                ";
7680 PRINT #1, ESC$; "L"; CHR$(1); CHR$(5);
7690 FOR COL% = 0 TO 79
7700 H% = COL% + (80 * ROW%)
7710 C% = PEEK(8192 + H%)
7720 PRINT #1, CHR$(COL1(C%)); CHR$(COL1(C%)); CHR$(COL2(C%)); CHR$(COL2(C%)); CHR$(COL3(C%)); CHR$(COL3(C%)); CHR$(COL4(C%)); CHR$(COL4(C%));
7725 PRINT #1, CHR$(COL5(C%)); CHR$(COL5(C%)); CHR$(COL6(C%)); CHR$(COL6(C%)); CHR$(COL7(C%)); CHR$(COL7(C%)); CHR$(COL8(C%)); CHR$(COL8(C%));
7730 NEXT COL%
7740 PRINT #1, CHR$(0): LPRINT
7745 NEXT G
7750 NEXT ROW%
7760 PRINT #1, CHR$(10); CHR$(10)
7765 PRINT #1, ESC$; "@"
7780 GOSUB 9010
7790 CLOSE #1: RETURN
8000 REM *************************
8010 REM *   CRIACAO DA MATRIZ   *
8020 REM *************************
8030 CLS
8040 LOCATE 3, 3: PRINT " GERANDO MATRIZ DE IMPRESSAO ..."
8060 A%(1) = 128: A%(2) = 64: A%(3) = 32: A%(4) = 16: A%(5) = 8: A%(6) = 4: A%(7) = 2: A%(8) = 1
8070 FOR I = 0 TO 255
8080 B% = I
8090 FOR J = 1 TO 8
8100 C% = B% - A%(J)
8110 IF C% < 0 THEN GOTO 8220
8120 ON J GOTO 8130, 8140, 8150, 8160, 8170, 8180, 8190, 8200
8130 COL1(I) = 1: GOTO 8210
8140 COL2(I) = 1: GOTO 8210
8150 COL3(I) = 1: GOTO 8210
8160 COL4(I) = 1: GOTO 8210
8170 COL5(I) = 1: GOTO 8210
8180 COL6(I) = 1: GOTO 8210
8190 COL7(I) = 1: GOTO 8210
8200 COL8(I) = 1
8210 B% = C%
8220 NEXT J
8230 NEXT I
8240 RETURN
8500 REM **********************
8510 REM *  TIPO DE GRAFICO   *
8520 REM **********************
8525 IF (TP(GRAF) = -1 AND TL(GRAF) = -1) THEN RETURN
8530 IF TP(GRAF) = 0 OR TP(GRAF) = -1 THEN GOTO 8640
8540 IF (TP(GRAF) <> 0 AND TL(GRAF) <> 0) THEN GOTO 8730
8550 ON TP(GRAF) GOSUB 8570, 8580, 8610
8560 RETURN
8570 PSET (XX, YY): PSET (XX1, YY1): RETURN
8580 PSET (XX, YY): PSET (XX + 1, YY): PSET (XX - 1, YY): PSET (XX, YY - 1): PSET (XX, YY + 1)
8590 PSET (XX1, YY1): PSET (XX1 + 1, YY1): PSET (XX1 - 1, YY1): PSET (XX1, YY1 - 1): PSET (XX1, YY1 + 1)
8600 RETURN
8610 PSET (XX, YY + 1): PSET (XX - 1, YY + 1): PSET (XX - 1, YY): PSET (XX - 1, YY - 1): PSET (XX, YY - 1): PSET (XX + 1, YY - 1): PSET (XX + 1, YY): PSET (XX + 1, YY + 1)
8620 PSET (XX1, YY1 + 1): PSET (XX1 - 1, YY1 + 1): PSET (XX1 - 1, YY1): PSET (XX1 - 1, YY1 - 1): PSET (XX1, YY1 - 1): PSET (XX1 + 1, YY1 - 1): PSET (XX1 + 1, YY1): PSET (XX1 + 1, YY1 + 1)
8630 RETURN
8640 REM *********   TIPO DE LINHAS  ***********
8645 IF TL(GRAF) = -1 THEN RETURN
8650 ON TL(GRAF) GOSUB 8670, 8680, 8710
8660 RETURN
8670 LINE (XX, YY)-(XX1, YY1): RETURN
8680 LINE (XX, YY)-(XX1, YY1)
8690 LINE (XX, YY - 1)-(XX1, YY1 - 1)
8700 RETURN
8710 IF MUDA = 0 THEN LINE (XX, YY)-(XX1, YY1): MUDA = 1: RETURN
8720 IF MUDA = 1 THEN PSET (XX, YY): PSET (XX - 1, YY + 1): PSET (XX - 1, YY - 1): PSET (XX + 1, YY - 1): PSET (XX + 1, YY + 1): PSET (XX1, YY1): PSET (XX1 - 1, YY1 + 1): PSET (XX1 - 1, YY1 - 1): PSET (XX1 + 1, YY1 - 1): PSET (XX1 + 1, YY1 + 1): MUDA = 0: RETURN
8730 REM ******  LINE AND POINT  ************
8740 IF TL(GRAF) <> -1 THEN GOSUB 8745
8742 IF TP(GRAF) <> -1 THEN GOSUB 8750
8743 RETURN
8745 ON TL(GRAF) GOSUB 8670, 8680, 8710
8746 RETURN
8750 ON TP(GRAF) GOSUB 8570, 8580, 8610
8760 RETURN
9010 CLS : LOCATE 5, 3: PRINT " CONFIRME A IMPRESSAO DA LEGENDA (S/N): ";
9020 Z$ = INKEY$: IF Z$ = "" THEN GOTO 9020
9030 IF Z$ <> "N" AND Z$ <> "S" THEN GOTO 9020
9040 IF Z$ = "N" THEN RETURN
9050 LOCATE 9, 3: PRINT "AJUSTE A FOLHA NA IMPRESSORA E TECLE <ENTER>"
9060 Z$ = INKEY$: IF Z$ = "" THEN GOTO 9060
9070 Z = ASC(Z$): IF Z <> 13 THEN GOTO 9060
9080 LOCATE 13, 3: PRINT "PARA A IMPRESSAO DA LEGENDA": LOCATE 15, 3: PRINT "TECLE <<CTRL><ESC>> APOS A TELA ESTAR PRESENTE NO VIDEO "
9100 LOCATE 19, 3: PRINT " TECLE <ESPACE> PARA LEGENDA": RETURN
9500 REM ***********************
9510 REM *  INICIO DO HARDCOPY *
9520 REM ***********************
9530 OPEN "LPT1:" FOR RANDOM AS #1: WIDTH #1, 255
9540 LPRINT SPC(16); TE$(7): ESC$ = CHR$(27): DEF SEG = &HB800
9550 PRINT #1, ESC$; "3"; CHR$(3); CHR$(10)
9560 FOR ROW% = 0 TO 99
9570 IF ROW% = 2 THEN COMP = LEN(STR$(MG(2, 2))): V$ = SPACE$(15 - COMP): LPRINT V$; MG(2, 2); : GOTO 9600
9580 IF ROW% = 97 THEN COMP = LEN(STR$(MG(2, 1))): V$ = SPACE$(15 - COMP): LPRINT V$; MG(2, 1); : GOTO 9600
9590 LPRINT "                ";
9600 PRINT #1, ESC$; "L"; CHR$(130); CHR$(2);
9610 FOR COL% = 0 TO 79
9620 H% = COL% + (80 * ROW%)
9630 C% = PEEK(H%)
9640 PRINT #1, CHR$(COL1(C%)); CHR$(COL2(C%)); CHR$(COL3(C%)); CHR$(COL4(C%)); CHR$(COL5(C%)); CHR$(COL6(C%)); CHR$(COL7(C%)); CHR$(COL8(C%));
9650 NEXT COL%
9660 PRINT #1, CHR$(0); CHR$(0): LPRINT
9670 LPRINT "                ";
9680 PRINT #1, ESC$; "L"; CHR$(130); CHR$(2);
9690 FOR COL% = 0 TO 79
9700 H% = COL% + (80 * ROW%)
9710 C% = PEEK(8192 + H%)
9720 PRINT #1, CHR$(COL1(C%)); CHR$(COL2(C%)); CHR$(COL3(C%)); CHR$(COL4(C%)); CHR$(COL5(C%)); CHR$(COL6(C%)); CHR$(COL7(C%)); CHR$(COL8(C%));
9730 NEXT COL%
9740 PRINT #1, CHR$(0); CHR$(0): LPRINT
9750 NEXT ROW%
9760 PRINT #1, CHR$(10); CHR$(10); ESC$; "@"
9770 COMP = LEN(STR$(MG(1, 1))): V$ = SPACE$(15): LPRINT V$; MG(1, 1); SPC(48 - COMP); MG(1, 2): LPRINT SPC(67 - LEN(TE$(6))); TE$(6)
9775 GOSUB 9010
9780 CLOSE #1: RETURN

